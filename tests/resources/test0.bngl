# A simple model of Michaelis-Menten kinetics using rate approximation

begin parameters
  # Cell volume
  V 1 # um^3 = fL
  # Avogadro's number- scaled for uM concentration and volume units of um^3
  NaV 602*V
  # Rate constants
  kf   1.0/(NaV) # 1/uM 1/s-> 1/molec 1/s
  kr   1.0e-1 # 1/s
  kcat 1.0e-2 # 1/s
  Km   (kcat + kr)/kf

  # Initial concentrations
  E0 0.01*NaV # uM -> molec / cell
  S0 1.0*NaV # uM -> molec / cell
end parameters

begin molecule types
  E(s)
  S(Y~0~P)
end molecule types

begin seed species
  E(s) E0
  S(Y~0) S0
end seed species

begin observables
  Molecules Su S(Y~0)
  Molecules Sp S(Y~P)
  Molecules ES E(s!1).S(Y!1)
end observables

begin functions
  MMrate() kcat*E0*Su/(Km + Su)
end functions

begin reaction rules
ES_MM:\
  E(s) + S(Y~0) -> E(s) + S(Y~P) kcat/(Km + Su) #MMrate()
end reaction rules